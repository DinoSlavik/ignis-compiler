# 5. Складені типи даних: Структури та Вказівники
Ignis дозволяє створювати складніші структури даних за допомогою `struct` (структур) та безпосередньо керувати пам'яттю за допомогою вказівників.

## Структури (`struct`)
Структури дозволяють групувати кілька змінних (полів) в один логічний тип даних. Це основний інструмент для створення власних типів.

### Оголошення структури
Структура оголошується за допомогою ключового слова `struct`, за яким слідує ім'я типу та блок з полями у фігурних дужках.

```Ignis

struct Point {
    int x;
    int y;
}
```

### Створення та використання
Після оголошення структури ви можете створювати її екземпляри (змінні) та працювати з її полями через оператор . (крапка).

```Ignis

// Створюємо мутабельну змінну типу Point
mut Point p;

// Присвоюємо значення полям
p.x = 10;
p.y = 20;

print(p.x); // Виведе 10
```

Варто зазначити, що структури в Ignis є типами-значеннями (`value types`). Це означає, що при присвоєнні однієї структурної змінної іншій створюється повна копія.

## Вказівники (`ptr`)
Вказівники — це змінні, які зберігають адресу в пам'яті іншої змінної. Вони дозволяють опосередковано доступатися до даних та модифікувати їх.

Ключові оператори для роботи з вказівниками:

- `ptr`: Використовується для оголошення типу вказівника (наприклад, `ptr int` — вказівник на `int`).

- `addr`: Унарний оператор, що повертає адресу змінної в пам'яті.

- `deref`: Унарний оператор, що дозволяє отримати або змінити значення, на яке вказує вказівник (розіменування).

```Ignis

mut int x = 10;

// 'p' - це вказівник, що зберігає адресу 'x'
mut ptr int p = addr x;

// Розіменовуємо вказівник, щоб прочитати значення 'x'
print(deref p); // Виведе 10

// Розіменовуємо, щоб змінити значення 'x' через вказівник
deref p = 42;

print(x); // Виведе 42, оскільки 'p' вказував на 'x'
```

### Вказівники та Структури
Вказівники особливо корисні при роботі зі структурами, оскільки дозволяють передавати у функції посилання на великі об'єкти, уникаючи дорогого копіювання.

Доступ до полів через вказівник
Ignis спрощує роботу з вказівниками на структури. На відміну від C/C++, вам не потрібен спеціальний оператор (->). Оператор . (крапка) працює як для самих структур, так і для вказівників на них. Компілятор автоматично розуміє, коли потрібно розіменувати вказівник.

```Ignis

struct GameObject {
    ptr Vector2D position;
    char id;
}

// Ця функція приймає вказівник на GameObject
void move_game_object(ptr GameObject obj, int dx, int dy) {
    // Доступ до полів через вказівник виглядає так само,
    // як і прямий доступ. Компілятор сам робить "->".
    obj.position.x = obj.position.x + dx;
    obj.position.y = obj.position.y + dy;
}
```

// TODO: При оголошенні структури у кінці певно треба додати ';'. Хоча, з іншого боку, без крапки з комою все вигляда значно чистіше.
